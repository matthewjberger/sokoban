set windows-shell := ["powershell.exe"]
export RUST_LOG := "info,wgpu_core=off"
export RUST_BACKTRACE := "1"

# Displays the list of available commands
@just:
    just --list

# Builds the project in release mode
build:
    cargo build -r

# Runs cargo check and format check
check:
    cargo check --all --tests
    cargo fmt --all -- --check

# Generates and opens documentation
docs:
    cargo doc --open

# Fixes linting issues automatically
fix:
    cargo clippy --all --tests --fix

# Formats the code using cargo fmt
format:
    cargo fmt --all

# Install wasm tooling
init-wasm:
    rustup target add wasm32-unknown-unknown
    cargo install --locked trunk

# Runs linter and displays warnings
lint:
    cargo clippy --all --tests -- -D warnings

# Runs the app natively
run:
    cargo run -r

# Solves every shipped board and checks it against its recorded par
analyze budget="":
    cargo run -r -- analyze {{ budget }}

# Generates boards from every preset and every hazard setting
random count="3":
    cargo run -r -- random {{ count }}

# Plays every gallery demonstration through the rules
lessons:
    cargo run -r -- lessons

# Solves every gallery board and writes the worked example back into the file
demos:
    cargo run -r -- demos

# Reads the campaign in order and says what each board is about
story:
    cargo run -r -- story

# Runs every data check the shipped boards answer to
gate:
    just lint
    cargo run -r -- analyze
    cargo run -r -- lessons
    cargo run -r -- depot
    cargo run -r -- characters

# Build the app for WASM
build-wasm:
    trunk build --release

# Serve the app in browser
run-wasm:
    trunk serve --release --open

# Runs all tests
test:
    cargo test --all -- --nocapture

# Checks for unused dependencies
udeps:
    cargo machete

# Prints a table of all dependencies and their licenses
licenses:
    cargo license

# Checks for problematic licenses in dependencies
licenses-check:
    cargo deny check licenses

# Generates third-party license attribution document
licenses-html:
    cargo about generate about.hbs -o THIRD_PARTY_LICENSES.html

# Vendors all dependencies into the vendor directory
vendor:
    cargo vendor

# Install development tools
install-tools:
    cargo install --locked cargo-license
    cargo install --locked cargo-about
    cargo install --locked cargo-deny
    cargo install --locked cargo-machete

# Displays version information for Rust tools
@versions:
    rustc --version
    cargo fmt -- --version
    cargo clippy -- --version

# Watches for changes and runs the app
watch:
    cargo watch -x 'run -r'

# Install Steam Deck cross-compilation tooling (cross from git for rustup 1.28+ support; needs Docker running)
init-steamdeck:
    cargo install cross --git https://github.com/cross-rs/cross --rev 29d00c7 --locked

# Builds the project for Steam Deck using cross (run `just init-steamdeck` first)
build-steamdeck:
    cross build --release --target x86_64-unknown-linux-gnu

# Builds and deploys the project to Steam Deck
deploy-steamdeck:
    just build-steamdeck
    scp ./target/x86_64-unknown-linux-gnu/release/sokoban deck@steamdeck.local:~/Downloads

# Quick deploy to Steam Deck (renames to 'game' for easy launching)
deploy-steamdeck-quick:
    just build-steamdeck
    scp ./target/x86_64-unknown-linux-gnu/release/sokoban deck@steamdeck.local:~/Downloads/game
    ssh deck@steamdeck.local "chmod +x ~/Downloads/game"
